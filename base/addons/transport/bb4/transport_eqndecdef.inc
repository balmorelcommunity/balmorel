* Transport add-on. Added by Juan Gea Bermudez



EQUATION  QTRANSPEAKDEMAND(Y,RRR,S,T)   'Peak demand (MW)';

QTRANSPEAKDEMAND(IY411,IR,IS3,T)$SUM(C,TRANSDEMAND_Y(IY411,C))..
VTRANSDEMAND_T_MAX(IY411,IR)
=G=
VTRANSDEMAND_T(IY411,IR,IS3,T)

$include "../../base/addons/_hooks/qtranspeakdemand.inc"
;


$ifi %import_results%==yes $ifi %ADDSEASONALTRANSPORTDEMAND%==yes     $goto $SEASONALTRANSPORTDEMAND
EQUATION  QTRANSDEMAND_Y(Y)    'Annual transport demand has to be met (MWh)';

QTRANSDEMAND_Y(IY411)$SUM(C,TRANSDEMAND_Y(IY411,C))..
SUM((C,IR)$CCCRRR(C,IR),
   SUM((IS3,T),
      VTRANSDEMAND_T(IY411,IR,IS3,T)*IHOURSINST(IS3,T))
)
=E=
SUM(C,TRANSDEMAND_Y(IY411,C))
;

EQUATION  QTRANSPEAKDEMANDLIMIT(Y,RRR)    'Peak demand limited(MW)';

QTRANSPEAKDEMANDLIMIT(IY411,IR)$((TRANS_PEAKAVG_RATIO GT 0) AND SUM(C,TRANSDEMAND_Y(IY411,C)))..
SUM((IS3,T),VTRANSDEMAND_T(IY411,IR,IS3,T))/SUM((IS3,T),1)*TRANS_PEAKAVG_RATIO
 =G=
VTRANSDEMAND_T_MAX(IY411,IR)
;
$label $SEASONALTRANSPORTDEMAND

$ifi not %import_results%==yes $goto $NO_SEASONALTRANSPORTDEMAND
$ifi     %import_results%==yes $ifi not %ADDSEASONALTRANSPORTDEMAND%==yes  $goto $NO_SEASONALTRANSPORTDEMAND

EQUATION  QTRANSDEMAND_S(Y,S)    'Seasonal transport demand has to be met (MWh)';

QTRANSDEMAND_S(IY411,IS3)$SUM(C,TRANSDEMAND_Y(IY411,C))..
SUM((C,IR)$CCCRRR(C,IR),
   SUM(T,
      WEIGHT_S(IS3)/IWEIGHSUMT * VTRANSDEMAND_T(IY411,IR,IS3,T))
)
=E=
SUM(C,TRANSDEMAND_S(IY411,C,IS3))
;

$label $NO_SEASONALTRANSPORTDEMAND

$ifi not %import_results%==yes $goto $NO_TERMTRANSPORTDEMAND
$ifi     %import_results%==yes $ifi not %ADDTERMTRANSPORTDEMAND%==yes  $goto $NO_TERMTRANSPORTDEMAND

EQUATION  QTRANSDEMAND_T(Y,RRR,S,T)    'Transport demand per term has to be met (MWh)';

QTRANSDEMAND_T(IY411,IR,IS3,T)..
VTRANSDEMAND_T(IY411,IR,IS3,T)
=E=
TRANSDEMAND_T(IY411,IR,IS3,T)
;

$label $NO_TERMTRANSPORTDEMAND


